plugins {
    id 'maven-publish'
    alias libs.plugins.minotaur
    alias libs.plugins.curseforgegradle
}

apply plugin: 'org.quiltmc.loom'

base.archivesName = "${artifact_id}-fabriquilt-${libs.versions.minecraft.get()}"

configurations {
    testCompileClasspath.extendsFrom(compileClasspath)
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${libs.versions.parchment.minecraft.get()}:${libs.versions.parchment.mappings.get()}@zip")
    }
    modImplementation libs.quilt.loader
    modImplementation libs.quilt.qfapi

    implementation project(":Common")

    compileOnly libs.autoservice
    annotationProcessor libs.autoservice

    include(implementation(annotationProcessor(libs.mixinextras.fabric.get())))
}

loom {
    runs {
        client {
            client()
            setConfigName("Quilt Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Quilt Server")
            ideConfigGenerated(true)
            runDir("runserver")
        }
    }

    mixin {
        defaultRefmapName = "worldgenflexiblifier.refmap.json"
    }
}

processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("quilt.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}"}
    }
}

tasks.jar.finalizedBy(tasks.remapJar)

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId base.archivesName.get()
            version project.version
            from components.java
        }
    }

    repositories {
    }
}

import net.darkhax.curseforgegradle.TaskPublishCurseForge;

if (System.getenv('CURSEFORGE_KEY')) {
    task curseforge(type: TaskPublishCurseForge) {
        disableVersionDetection()
        apiToken = System.getenv('CURSEFORGE_KEY')
        def projectId = '909775'
        def mainFile = upload(projectId, tasks.remapJar)
        mainFile.displayName = "Worldgen Flexiblifier [Fabric/Quilt] v$project.version"
        mainFile.releaseType = "${project.release_type}"
        mainFile.addModLoader('Fabric','Quilt')
        mainFile.addGameVersion("${libs.versions.minecraft.get()}")
        mainFile.addRequirement("fabric-api")
        mainFile.changelog = ''
    }

    afterEvaluate {
        tasks.curseforge.dependsOn tasks.remapJar
    }
}

if (System.getenv('MODRINTH_KEY')) {
    modrinth {
        token = System.getenv('MODRINTH_KEY')
        projectId = 'zk8VT5Nr'
        versionNumber = "$project.version"
        versionType = "${project.release_type}"
        detectLoaders = false
        uploadFile = tasks.remapJar
        gameVersions = ["${libs.versions.minecraft.get()}"]
        loaders = ['fabric','quilt']
        dependencies {
            required.project 'fabric-api'
        }
    }

    afterEvaluate {
        tasks.modrinth.dependsOn tasks.remapJar
    }
}
